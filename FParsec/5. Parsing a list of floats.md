# Разбор списка чисел с плавающей точной

Мы уже провели три главы в обсуждении того, как разобрать единственное число с плавающей точкой, так что пришло время попробовать что-то более амбициозное - разбор списка чисел с плавающей точкой.

Давайте вначале предположим, что нам нужно распарсить последовательность чисел с плавающей точкой в скобках, т.е. текст в следующем формате `EBNF`: `("[" float "]")*`. Корректными строками для этого формата будут, например, такие: `""`, `"[1.0]"`, `"[2][3][4]"`.  

Поскольку у нас уже был парсер для числа типа `float` в скобках, нам нужен только способ неоднократно применить этот парсер к разбору последовательности. Это как раз то, что делает комбинатор [`many`](http://www.quanttec.com/fparsec/reference/primitives.html#members.many):

```fsharp
val many: Parser<'a,'u> -> Parser<'a list,'u>
```

Парсер `many p` многократно применяет парсер `p` до тех пор пока `p` не закончится неудачей, другими словами он "жадно" парсит столько встречаемостей `p` сколько возможно. Результат возвращается в виде списка в порядке встречаемости.

Несколько простых тестов показывают, что `many floatInBrackets` работает как и ожидалось:

```fsharp
> test (many floatBetweenBrackets) "";;
Success: []
> test (many floatBetweenBrackets) "[1.0]";;
Success: [1.0]
> test (many floatBetweenBrackets) "[2][3][4]";;
Success: [2.0; 3.0; 4.0]
```

Если `floatBetweenBrackets` оканчивается неудачей после поглощения каких-либо входных данных, тогда общий парсер тоже будет провален.

```fsharp
> test (many floatBetweenBrackets) "[1][2.0E]";;
Failure: Error in Ln: 1 Col: 9
[1][2.0E]
        ^
Expecting: decimal digit
```

Отметим, что `many` также успешен для пустой последовательности. Если вы хотите потребовать наличие хотя-бы одного элемента, вы можете использовать для этого [`many1`](http://www.quanttec.com/fparsec/reference/primitives.html#members.many1):

```fsharp
> test (many1 floatBetweenBrackets) "(1)";;
Failure: Error in Ln: 1 Col: 1
(1)
^
Expecting: '['
```

**_Примечание_**

> Если вы предпочитаете, чтобы последнее сообщение об ошибке было записано в терминах высокоуровневого парсера `floatBetweenBrackets` вместо низкоуровневого `str "["` вы можете использовать оператор `<?>` как в следующем примере:
>```fsharp
>> test (many1 (floatBetweenBrackets <?> "float between brackets")) "(1)";;
>Failure: Error in Ln: 1 Col: 1
>(1)
>^
>Expecting: float between brackets
>```
> Пожалуйста, загляните в [раздел 5.8](http://www.quanttec.com/fparsec/users-guide/customizing-error-messages.html) из руководства пользователя, чтобы узнать больше о кастомизации сообщений об ошибках.

Если вы хотите пропустить последовательность и вам не нужен список результатов парсинга, вы можете использовать оптимизированный комбинатор [`skipMany`](http://www.quanttec.com/fparsec/reference/primitives.html#members.skipMany) и [`skipMany1`](http://www.quanttec.com/fparsec/reference/primitives.html#members.skipMany1) вместо `many` и `many1` соответственно.

Другой часто используемый комбинатор для разбора последовательностей это [`sepBy`](http://www.quanttec.com/fparsec/reference/primitives.html#members.sepBy):

```fsharp
val sepBy: Parser<'a,'u> -> Parser<'b,'u> -> Parser<'a list, 'u>``
```

`sepBy` принимает парсер для элемента и парсер для разделителя в качестве аргументов и возвращает парсер для списка элементов отделенных разделителем. В EBNF нотации `sepBy p pSep` может быть записан как `(p (pSep p)*)?`. По аналогии с `many` есть несколько вариантов для `sepBy`.

С помощью `sepBy` мы можем разбирать более читабельный формат списка, где числа с плавающей точкой разделены запятыми: `floatList: "[" (float ("," float)*)? "]"`

Валидными входными строками в этом случае будут, например, такие:

`"[]"`, `"[1.0]"`, `"[2,3,4]"`

Непосредственная реализация этого формата:

```fsharp
let floatList = str "[" >>. sepBy pfloat (str ",") .>> str "]"
```

Проверка `floatList` с корректными тестовыми строками возвращает ожидаемый результат

```fsharp
> test floatList "[]";;
Success: []
> test floatList "[1.0]";;
Success: [1.0]
> test floatList "[4,5,6]";;
Success: [4.0; 5.0; 6.0]
```

Проверка для невалидных строк, показывает, что `floatList` производит полезные сообщения об ошибках:

```fsharp
> test floatList "[1.0,]";;
Failure: Error in Ln: 1 Col: 6
[1.0,]
     ^
Expecting: floating-point number
> test floatList "[1.0,2.0";;
Failure: Error in Ln: 1 Col: 9
[1.0,2.0
        ^
Note: The error occurred at the end of the input stream.
Expecting: ',' or ']'

```