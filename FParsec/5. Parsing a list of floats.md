# Parsing a list of floats

We've already spent three sections on discussing how to parse a single floating-point number, so it's about time we try something more ambitious: parsing a list of floating-point numbers.

Let us first assume that we need to parse a sequence of floating-point numbers in brackets, i.e. text in the following EBNF format: `{EBNF}("[" float "]")*`. Valid input strings in this format are for example: `""`, `"[1.0]"`, `"[2][3][4]"`.

Since we already have a parser for a float between brackets, we only need a way to repeatedly apply this parser to parse a sequence. This is what the [many](http://www.quanttec.com/fparsec/reference/primitives.html#members.many) combinator is for:

```fsharp
val many: Parser<'a,'u> -> Parser<'a list,'u>``
```

The parser `many p` repeatedly applies the parser `p` until `p` fails, i.e. it "greedily" parses as many occurrences of `p` as possible. The results of `p` are returned as a list in the order of occurrence.

Some simple tests show that `many floatInBrackets` works as expected:

```fsharp
> test (many floatBetweenBrackets) "";;
Success: []
> test (many floatBetweenBrackets) "[1.0]";;
Success: [1.0]
> test (many floatBetweenBrackets) "[2][3][4]";;
Success: [2.0; 3.0; 4.0]
```

If `floatBetweenBrackets` fails *after consuming input*, then the combined parser fails too:

```fsharp
> test (many floatBetweenBrackets) "[1][2.0E]";;
Failure: Error in Ln: 1 Col: 9
[1][2.0E]
        ^
Expecting: decimal digit
```

Note that `many` also succeeds for an empty sequence. If you want to require at least one element, you can use [many1](http://www.quanttec.com/fparsec/reference/primitives.html#members.many1) instead:

```fsharp
> test (many1 floatBetweenBrackets) "(1)";;
Failure: Error in Ln: 1 Col: 1
(1)
^
Expecting: '['
```

**_tip_**

>If you'd prefer the last error message to be worded in terms of the higher level `floatBetweenBrackets` parser instead of the lower level `str "["` parser, you could use the `<?>` operator as in the following example:
>```fsharp
>> test (many1 (floatBetweenBrackets <?> "float between brackets")) "(1)";;
>Failure: Error in Ln: 1 Col: 1
>(1)
>^
>Expecting: float between brackets
>```
> Please see [customizing-error-messages](http://www.quanttec.com/fparsec/users-guide/customizing-error-messages.html) of the user's guide to learn more about customizing error messages.

If you just want to skip over a sequence and don't need the list of parser results, you could use the optimized combinators [skipMany](http://www.quanttec.com/fparsec/reference/primitives.html#members.skipMany) or [skipMany1](http://www.quanttec.com/fparsec/reference/primitives.html#members.skipMany1) instead of `many` and `many1`.

Another frequently used combinator for parsing sequences is [`sepBy`](http://www.quanttec.com/fparsec/reference/primitives.html#members.sepBy):

```fsharp
val sepBy: Parser<'a,'u> -> Parser<'b,'u> -> Parser<'a list, 'u>``
```

`sepBy` takes an "element" parser and a "separator" parser as the arguments and returns a parser for a list of elements separated by separators. In EBNF notation `sepBy p pSep` could be written as `{EBNF}(p (pSep p)*)?`. Similar to `many`, there are [several variants](http://www.quanttec.com/fparsec/reference/primitives.html#interface.sepBy-parsers) of `sepBy`.

With the help of `sepBy` we can parse a more readable list format, where floating-point numbers are separated by a comma: `{EBNF}floatList: "[" (float ("," float)*)? "]"`

Valid input strings in this format are for example: 

`"[]"`, `"[1.0]"`, `"[2,3,4]"`.

The straightforward implementation of this format is

```fsharp
let floatList = str "[" >>. sepBy pfloat (str ",") .>> str "]"
```

Testing `floatList` with valid test strings yields the expected result:

```fsharp
> test floatList "[]";;
Success: []
> test floatList "[1.0]";;
Success: [1.0]
> test floatList "[4,5,6]";;
Success: [4.0; 5.0; 6.0]
```

Testing with invalid input shows that `floatList` produces helpful error messages:

```fsharp
> test floatList "[1.0,]";;
Failure: Error in Ln: 1 Col: 6
[1.0,]
     ^
Expecting: floating-point number

> test floatList "[1.0,2.0";;
Failure: Error in Ln: 1 Col: 9
[1.0,2.0
        ^
Note: The error occurred at the end of the input stream.
Expecting: ',' or ']'
```